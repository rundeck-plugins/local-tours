buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.7.0'
}

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //append .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}

defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
sourceCompatibility = 1.8
ext.rundeckPluginVersion= '2.0'
project.version = scmVersion.version

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.rundeck:rundeck-core:3.0.9-20181127'
    compile "org.slf4j:slf4j-api:1.7.25"

    testCompile 'junit:junit:4.12'
    testCompile "org.codehaus.groovy:groovy-all:2.4.15"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

ext.pluginClassNames='com.rundeck.plugin.localtours.LocalTours'
jar {
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Name': 'LocalTours'
        attributes 'Rundeck-Plugin-Description': 'Loads Tours From Filesystem.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.9+'
        attributes 'Rundeck-Plugin-Tags': 'java,tour loader'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}
